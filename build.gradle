buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'cz.alenkacz:gradle-scalafmt:1.5.0'
    }
}


plugins {
    id 'com.github.maiflai.scalatest' version '0.16'
    id 'com.jfrog.bintray' version '1.7.3'
}

allprojects {
    apply plugin: 'scala'
    apply plugin: 'scalafmt'
    apply plugin: 'idea'
    apply plugin: 'com.github.maiflai.scalatest'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'
    group = artifactGroup
    version = artifactVersion

    sourceSets {
        main {
            scala {
                srcDirs = ['src/main/scala', 'src/main/java']
            }
            java {
                srcDirs = []
            }
        }
    }

    repositories {
        mavenCentral()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {

        compile       group: 'org.scala-lang',             name: 'scala-library',    version: '2.12.6'
        compile       group: 'org.apache.kafka',           name: 'kafka-streams',    version: '1.1.1'
        compile scala(group: 'com.typesafe.scala-logging', name: 'scala-logging',    version: '3.7.2')
        compile scala(group: 'io.kamon',                   name: 'kamon-core',       version: '1.1.3')
        compile scala(group: 'io.kamon',                   name: 'kamon-prometheus', version: '1.1.1')


        testCompile       group: 'org.mockito',          name: 'mockito-core',             version: '2.8.47'
        testCompile       group: 'org.apache.kafka',     name: 'kafka-streams',            version: '1.1.1'
        testCompile       group: 'org.apache.zookeeper', name: 'zookeeper',                version: '3.4.10', ext: 'pom'
        testCompile       group: 'org.slf4j',            name: 'slf4j-log4j12',            version: '1.7.25'
        testCompile       group: 'log4j',                name: 'log4j',                    version: '1.2.17'
        testCompile scala(group: 'net.manub',            name: 'scalatest-embedded-kafka', version: '1.1.1')
        testCompile scala(group: 'org.scalatest',        name: 'scalatest',                version: '3.0.1')

        testRuntime group: 'org.pegdown',    name: 'pegdown',         version: '1.4.2'
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '4.1'
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    scalafmt {
        configFilePath = "${rootDir}/.scalafmt.conf"
    }

    tasks.check.dependsOn(checkScalafmtAll)
}

publishing {
    publications {
        KamonKafkaReporterPublication(MavenPublication) {
            from components.java
            groupId artifactGroup
            artifactId "kamon-kafka-reporter_${scalaVersion}"
            version artifactVersion
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = 'kamon-kafka-reporter'
        userOrg = 'and-schroeder'
    }
    publications = ['KamonKafkaReporterPublication']
}

project(':it') {
    dependencies {
        compile rootProject
        compile scala(group: 'org.scalatest',     name: 'scalatest',      version: '3.0.1')
        compile       group: 'org.mockito',       name: 'mockito-core',   version: '2.8.47'
    }
}

def scala(Object dep) { "${dep.group}:${dep.name}_${scalaVersion}:${dep.version}" }
